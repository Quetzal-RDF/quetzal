PREFIX : <http://example.org/> 

CONSTRUCT {?n a :OilDeclineVulnerableIndustry}
WHERE {
	?n (<http://www.w3.org/2004/02/skos/core#broader>)* ?c . 
} values ?c { <http://naicscodes/211> <http://naicscodes/213111>  <http://naicscodes/213112> <http://naicscodes/21114> <http://naicscodes/21115> <http://naicscodes/21116>
}

###
PREFIX : <http://example.org/> 

CONSTRUCT {?company a :OilDeclineVulnerableCompany}
WHERE {
	?company <http://rdf.ibm.com/dunsColumn/BUS_NAME> ?name . 
	?company <http://rdf.ibm.com/dunsColumn/NAICS_CD> ?n . 
	?n a :OilDeclineVulnerableIndustry .
	?company a <http://types.rdf.ibm.com/Company>
}

###
PREFIX : <http://example.org/> 

CONSTRUCT {?companyRegion a :OilDeclineVulnerableCompanyRegion}
WHERE {
	?company a :OilDeclineVulnerableCompany .
	?company <http://rdf.ibm.com/dunsColumn/FIPS_CD> ?companyRegion .
}

###
PREFIX : <http://example.org/> 
CONSTRUCT {?loan a :OilDeclineVulnerableLoan}
WHERE {
	?loan  a <http://markitLoan.sinatra.ibm.com/type#LXID> . 
	?loan  <http://markitLoan.sinatra.ibm.com/Deal_Name> ?dealName . 
	?company <http://www.w3.org/2000/01/rdf-schema#label> ?dealName . 
	?company a :OilDeclineVulnerableCompany
}
